CLEARSCREEN.
SET CONFIG:IPU TO 400.
SET TERMINAL:REVERSE TO TRUE.
// SET TERMINAL:WIDTH TO 40.
// SET TERMINAL:HEIGHT TO 20.

// ========================================================================

DECLARE TURNSPEED TO 80.
DECLARE TURNANGLE TO 10.
DECLARE TURNSTART TO 0.
DECLARE TURNEND TO 40000.
DECLARE FINALAP TO 80000.
DECLARE PITCH TO 90.

LOCK AIRSPD TO SHIP:AIRSPEED.
LOCK ORBSPD TO SHIP:VELOCITY:ORBIT:MAG.
LOCK ALT TO SHIP:ALTITUDE.
LOCK TTA TO ETA:APOAPSIS.

DECLARE PHASE TO 0.
// 0 - preflight
// 1 - climb
// 2 - turn
// 3 - throttle control
// 4 - level off
// 5 - in space
// 6 - final orbit

SAS OFF.
LOCK STEERING TO HEADING(90,90).
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.

PHASESHIFT(0).
WAIT UNTIL AIRSPD > 1.

// ========================================================================

WHEN AIRSPD > 3 		THEN {PHASESHIFT(1).}
WHEN AIRSPD >= TURNSPEED THEN {PHASESHIFT(2).}
WHEN TTA >= 40 		THEN {PHASESHIFT(3).}
WHEN ALT >= TURNEND 	THEN {PHASESHIFT(4).}
WHEN ALT >= 70000 		THEN {PHASESHIFT(5).}


// ========================================================================

SET apoapsis_setpoint TO 40.

LOCK P TO apoapsis_setpoint - TTA.
SET P0 TO P.
SET I TO 0.
SET D TO 0.
SET Kp TO 0.156.
SET Ki TO 0.101.
SET Kd TO 0.060.
LOCK in_deadband TO ABS(P) < 0.01.
LOCK dthrott TO Kp * P + Ki * I + Kd * D.
SET THROTT TO 0.

SET t0 TO TIME:SECONDS.

WHEN PHASE > 0 AND PHASE < 4 THEN {
	IF PHASE < 4 {PRESERVE.}
	SET dt TO TIME:SECONDS - t0.
	IF dt > 0 {
		IF NOT in_deadband {
			SET I TO I + P * dt.
			SET D TO (P - P0) / dt.

			// If Ki is non-zero, then limit Ki*I to [-1,1]
			IF Ki > 0 {
				SET I TO MIN(1.0/Ki, MAX(-1.0/Ki, I)).
			}

			// set throttle but keep in range [0,1]
			SET SHIP:CONTROL:PILOTMAINTHROTTLE to MIN(1, MAX(0, thrott + dthrott)).

			SET P0 TO P.
			SET t0 TO TIME:SECONDS.
		}
	}
	PRINT "THROTTLING @ " + ROUND(T0,2) AT(2,2).
}

WHEN PHASE > 1 AND PHASE < 4 THEN {
	IF PHASE < 4 {PRESERVE.}
	SET PITCH TO (90 - TURNANGLE) * (ALT - TURNSTART) / (TURNEND - TURNSTART).
	LOCK STEERING TO HEADING(90, 90 - TURNANGLE - PITCH).
	PRINT "STEERING @ " + ROUND(TURNANGLE + PITCH,2) AT(2,3).
}

WAIT UNTIL PHASE = 4.


// ========================================================================

// WHEN PHASE = 4 THEN {
// 	THROTTLE CONTROL TO KEEP APOAPSIS UP	
// 	IF PHASE = 4 {PRESERVE.}
// }

UNLOCK STEERING.
SAS ON.
WAIT 1.
SET SASMODE TO "PROGRADE".

WAIT UNTIL ORBIT:APOAPSIS > FINALAP.
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
SAS OFF.

WAIT UNTIL PHASE = 5.


// ========================================================================

SET WARP TO 0.
WAIT 0.1.
UNTIL ORBIT:APOAPSIS > FINALAP {
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.1.
}
SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
WAIT 3.

SET GK TO 3.5316000*10^12.
SET RADIUS TO 600000 + APOAPSIS.
SET SMA TO 600000 + ((ORBIT:PERIAPSIS + ORBIT:APOAPSIS) / 2).
SET V1 TO (GK/RADIUS)^.5.
SET V2 TO (GK*((2/RADIUS)-(1/SMA)))^.5.
SET DV TO ABS(V1-V2).
SET ACCELERATION TO (MAXTHRUST/MASS).
SET BURNTIME TO (DV/ACCELERATION).
SET STARTTIME TO (BURNTIME/2).

SET WARP TO 2.
WAIT UNTIL ETA:APOAPSIS < (121 + STARTTIME).
SET WARP TO 1.
WAIT UNTIL ETA:APOAPSIS < (63 + STARTTIME).
SET WARP TO 0.
SAS ON.
WAIT 3.
SET SASMODE TO "PROGRADE".

SET CALT TO ORBIT:APOAPSIS - (ORBIT:APOAPSIS / 500).
SET XORBIT TO ORBIT:APOAPSIS + (ORBIT:APOAPSIS / 20).
SET XALT TO CALT - (CALT / 100).

PRINT " APO: " + ORBIT:APOAPSIS + "m" AT(2,4).
PRINT "PERI: " + ORBIT:PERIAPSIS + "m" AT(2,5).
PRINT "CALT: " + CALT + "m" AT(2,6).
PRINT "XALT: " + XALT + "m" AT(2,7).
PRINT "XORB: " + XORBIT + "m" AT(2,8).

WHEN ETA:APOAPSIS < STARTTIME THEN {
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 1.
}
WHEN ORBIT:PERIAPSIS > XALT THEN {
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO .1.
}
WHEN ORBIT:PERIAPSIS > CALT OR ORBIT:APOAPSIS > XORBIT THEN {
	SET SHIP:CONTROL:PILOTMAINTHROTTLE TO 0.
	SAS OFF.
	PHASESHIFT(6).
}

WAIT UNTIL PHASE = 6.


// ========================================================================

CLEARSCREEN.
PHASESHIFT(6).
PRINT " APO: " + ORBIT:APOAPSIS + "m" AT(2,4).
PRINT "PERI: " + ORBIT:PERIAPSIS + "m" AT(2,5).
PRINT "CALT: " + CALT + "m" AT(2,6).
PRINT "XALT: " + XALT + "m" AT(2,7).
PRINT "XORB: " + XORBIT + "m" AT(2,8).
PRINT "ORBIT ACHIEVED.  SAS OFF.  HAPPY SPACEFARING!" AT(2,13).


// ========================================================================

FUNCTION PHASESHIFT {
	PARAMETER VAL.
	
	CLEARSCREEN.
	PRINT "FLIGHT PHASE" AT(32,1).
	PRINT "1 2 3 4 5 6" AT(32,2).
	PRINT "*":PADLEFT(VAL*2) + " " AT(31,3).
	SET PHASE TO VAL.
}
